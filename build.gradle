buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.github.maiflai:gradle-scalatest:0.26'
        classpath "com.github.ben-manes:gradle-versions-plugin:0.29.0"
    }
}

ext {
    circeVersion = "0.13.0"
    metricsVersion = "2.7.5"
    http4sVersion = "0.21.7"
    monixVersion = "3.2.2" // Used only in tests.
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.github.maiflai.scalatest'
    apply plugin: "com.github.ben-manes.versions"


    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
        jcenter()
    }

    group = 'com.avast.clients.storage'
    version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version

    jar {
        manifest {
            attributes 'Implementation-Version': version
        }
    }

    dependencies {
        compile 'org.slf4j:jul-to-slf4j:1.7.23'
        compile 'org.slf4j:jcl-over-slf4j:1.7.23'
        compile 'com.typesafe.scala-logging:scala-logging_2.12:3.9.2'

        testCompile "org.http4s:http4s-blaze-server_2.12:$http4sVersion"

        testCompile "ch.qos.logback:logback-classic:1.2.3"
        testCompile 'junit:junit:4.13'
        testCompile "org.scalatest:scalatest_2.12:3.0.5"
        testCompile 'org.mockito:mockito-core:3.5.0'
        testCompile "org.pegdown:pegdown:1.6.0"
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier "sources"
    }

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                groupId project.group
                version project.version
                artifact sourceJar
            }
        }
    }

    artifacts {
        archives jar
        archives sourceJar
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publish = true // auto publish
        configurations = ['archives']

        pkg {
            repo = 'maven'
            name = 'storage-client'
            userOrg = 'avast'
            desc = 'Scala client for storage services'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/avast/storage-client.git'
            githubRepo = 'avast/storage-client'
            websiteUrl = 'https://github.com/avast/storage-client'
            issueTrackerUrl = 'https://github.com/avast/storage-client/issues'
            labels = ['stor', 'hcp', 's3', 'client', 'storage', 'scala']
            version {
                name = project.version
                vcsTag = project.version
            }
        }
    }

}
