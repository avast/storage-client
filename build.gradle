plugins {
    id 'org.jreleaser' version '1.22.0'
}

ext {
    metricsVersion = "3.0.6"
    http4sVersion = "0.22.15"
    gcsVersion = "2.61.0"
    zstdVersion = "1.5.7-6"
    monixVersion = "3.4.1" // Used only in tests.
}

tasks.register('clean') {}

allprojects {
    group = 'com.avast.clients.storage'
    version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jreleaser'

    repositories {
        mavenCentral()
    }

    jar {
        manifest {
            attributes 'Implementation-Version': archiveVersion
        }
    }

    dependencies {
        api 'org.slf4j:jul-to-slf4j:2.0.17'
        api 'org.slf4j:jcl-over-slf4j:2.0.17'
        api 'com.typesafe.scala-logging:scala-logging_2.13:3.9.6'

        testImplementation "org.http4s:http4s-blaze-server_2.13:$http4sVersion"

        testImplementation "ch.qos.logback:logback-classic:1.5.24"
        testImplementation 'junit:junit:4.13.2'
        testImplementation "org.scalatest:scalatest_2.13:3.0.9"
        testImplementation 'org.mockito:mockito-core:5.21.0'
        testImplementation "org.pegdown:pegdown:1.6.0"
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }

    afterEvaluate {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from(components.java)
                    artifactId = tasks.jar.archiveBaseName.get()
                    pom {
                        name = 'Scala storage client'
                        description = 'Finally-tagless implementation of client for misc. storages represented by backends. Supports backends fallbacks.'
                        url = 'https://github.com/avast/storage-client'
                        licenses {
                            license {
                                name = 'Apache License 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0'
                            }
                        }
                        developers {
                            developer {
                                id = 'jendakol'
                                name = 'Jenda Kolena'
                                email = 'jan.kolena@avast.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:git://github.com/avast/storage-client.git'
                            developerConnection = 'scm:git:ssh://github.com/avast/storage-client.git'
                            url = 'https://github.com/avast/storage-client'
                        }
                    }
                }
            }
            repositories {
                maven {
                    url = layout.buildDirectory.dir('staging-deploy')
                }
            }
        }
    }

    jreleaser {
        signing {
            active = 'ALWAYS'
            armored = true
            String base64Key = System.getenv('SIGNING_KEY')
            if (base64Key) {
                secretKey = new String(Base64.decoder.decode(base64Key))
            }
            String base64PublicKey = System.getenv('SIGNING_PUBLIC_KEY')
            if (base64PublicKey) {
                publicKey = new String(Base64.decoder.decode(base64PublicKey))
            }
        }
        deploy {
            maven {
                mavenCentral {
                    sonatype {
                        active = 'ALWAYS'
                        url = 'https://central.sonatype.com/api/v1/publisher'
                        stagingRepository('build/staging-deploy')
                    }
                }
            }
        }
        release {
            github {
                enabled = false
            }
        }
    }

}
